# 이 파일에 게임 스크립트를 입력합니다.

# image 문을 사용해 이미지를 정의합니다.
image black = "bg_black.png"
image sea = "bg_sea.jpg"
image river = "bg_river.jpg"

# 게임에서 사용할 캐릭터를 정의합니다.
define ch_narrator = Character(None)

define ch_junghoon = Character('정훈', color='#848484')

define ch_leewan = Character('???', color='#74006c')
define ch_leeyeon = Character('???', color='#38610B')

define ch_steward = Character('승무원')
define ch_courtlady = Character('상궁')

#자동 저장 허가
define config.has_autosave = True
define autosave_on_choice = True

# 여기에서부터 게임이 시작합니다.
label start:
    scene black
    centered "끝은 새로운 시작이니, 누군가는 그 길을 가야 한다." with fade

    scene sea
    play sound "audio/waving.mp3" volume 0.5

    "갑판에 섰다."
    "짜디짠 바닷바람이 숨통을 조이듯 밀려든다."
    "감회가 새로울 거란 예상과 달리, 눈앞에 펼쳐진 광경에 모든 기대가 무너졌다."

    ch_junghoon "그대로네..."

    "십 년 전이나 지금이나, 조선은 다를 게 없었다."
    "어째서 달라진 게 아무것도 없나."
    "저 바다 건너 세상은 하루가 멀다하고 새로워지는데."

    ch_junghoon "......"

    "짙은 한숨이 싸늘한 공기 위로 흩어졌다."
    "뒤로 다가오는 인기척이 느껴져 습관적으로 뒤를 돌았다."

    ch_steward "배가 곧 정박합니다. 선실로 들어가시지요."

    "갑판에 있던 다른 손님들이 하나둘 사라졌다."
    "청의 언어를 할 수 있는 건 아니지만, 대충 눈치로 알아들을 수 있었다."
    "정훈은 가볍게 목례하고 선실로 들어갔다."

    stop sound fadeout 1.0

    #scene hallway
    
    "좁은 복도를 따라 걸었다."
    "걸을 때마다 마룻바닥이 끼익 소리를 낸다."
    "맞은편 모서리를 돌아 대화소리가 들린다."

    ch_leeyeon "곧 눈이 올 모양입니다."
    ch_leewan "한바탕 장설이면 좋겠군."
    ch_leeyeon "이제 더는 청에 갈 일이 없으시겠지요?"
    ch_leewan "그래. …그 자와는 더 이상 할 말이 없어."

    "사람은 둘."    
    "중년 남자와 젊은 남자다."
    "걸음이 가까워진다."
    "정훈은 바닥에 꽂아두었던 시선을 올렸다."
    "콧수염 난 나이 든 남자가 이쪽을 바라보고 있다."
    "찰나 마주친 눈이 날카롭다."

    ch_leewan "......"
    ch_junghoon "......"

    "정훈은 그 눈을..."
    menu:
        "계속 바라본다.":
            "이완은 정훈을 바라보며 혀를 쯔, 찼다."
            
            ch_leewan "무도한 자군."
            
            "실로 오랜만에 들은 조선말인데, 본새가 말이 아니다."
            "정훈은 대충 상황을 모면하기 위해 고개를 숙이고 걸음을 옮겼다."
            "곁을 지나치려던 찰나, 팔이 붙들렸다."
            "젊은 남자 쪽이었다."

            ch_leeyeon "이 분이 누구신지 알고."

            "정훈은..."
            menu:
                "강경하게 나간다.":
                    ch_junghoon "...제가 알아야 합니까?"
                    ch_leeyeon "뭐야?"
                    ch_leewan "됐다."
                    "젊은 남자가 손찌검이라도 할 기세로 다가섰지만,"
                    "누군지 모르겠지만, 뭐 대단한 사람인지 뭔지 하는 사람이 그를 말렸다."
                    ch_leewan "패기가 마음에 드는군."
                    "난 당신이 마음에 들지 않는다."
                    ch_leewan "또 보지."

                "한 번 숙여준다.":
                    ch_junghoon "모릅니다만."
                    ch_leeyeon "뭐야?"
                    ch_leewan "그럴 줄 알았다. 내 앞에서 굽히지 않는 걸 보면."
                    "정훈은 서늘한 눈으로 그를 바라보았다."
                    "뭐, 왕이라도 된단 말인가?"
                    ch_leewan "허나 기억해 두게."
                    ch_junghoon "......"
                    ch_leewan "조선에서 나를 모르는 자는 수상쩍단 소릴 듣는다네."
                    ch_leewan "그만 가지."
                    "그들은 좁은 복도를 지나 선실로 들어가버렸다."

        "눈을 피한다.":
            "배가 정박하는 소리에 시선을 거두고 걸음을 옮겼다."
            "...신경 쓸 겨를은 없다."

    "정훈은 선실로 돌아갔다."
    "그리고는 품에 있는 전보를 생각했다."

    show black with dissolve
    centered "국왕이 속히 알현할 것을 명하다."
    centered "이건 분명 나에게 보낸 전보라고 했다."
    centered "나를 찾는다."
    centered "조선이 나를 찾는다." with fade
    hide black

    "정훈은 제 심정처럼 일렁이는 파도를 바라보았다."

    #scene harbor
    scene black with dissolve
    "배가 정박하는 소리에 걸음을 옮겼다."

    ch_steward "일등칸 손님부터 한 분씩 내리십쇼!"

    "삼등칸에 있던 이들은 다시 문을 닫고 들어갔다."
    "그들을 돌아보던 정훈은 작은 일수가방만 손에 쥔 채 출구로 향했다."
    "오랜만에 육지를 밟으니 발바닥이 허공이 뜬 느낌이다."
    "익숙하지 않은 걸음을 옮겼다."
    "선착장에 멍하니 서 있는 정훈에게 나이 든 여자가 다가왔다."
    "차림을 보니 궁인인 것 같았다."

    ch_courtlady "불란서의 전보를 받고 온 분이신지요."
    ch_junghoon "...그렇습니다."
    ch_courtlady "나루까지는 말을 타고 가셔야 합니다."
    menu:
        "...말을 탈 줄 모르는데.":
            pass
        "말을... 탈 줄 알아야 하는 거였군.":
            pass

    ch_junghoon "실은 제가...말을 탈 줄 모릅니다."
    "궁인은 눈을 크게 뜨고 한참 말이 없었다."
    "어색한 침묵이 흐르고 마침내 입을 열었다."

    ch_courtlady "...거기까진 생각지 못했군요."
    ch_courtlady "기마를 하란 게 아니니 안심하고 오르기만 하십시오."
    
    "더 할 말이 없어졌다."
    "정훈은 말 두 필 중 뒤에 있는 놈을 골라 탔다."
    "상궁이 말에 올라 박차를 가했다."
    "말이 천천히 움직이기 시작했다."

    #scene street

    "발 아래로 공기가 스친다."
    "지나다니는 사람들은 죄다 남루한 하얀 옷을 입고 있다."
    "장옷을 걸친 상궁은 고개도 돌리지 않은 채 말했다."

    ch_courtlady "주변에 시선을 주지 마십시오."
    ch_junghoon "...왜 그래야 합니까?"

    "상궁은 그제야 뒤돌아 냉랭한 눈을 떴다."

    ch_courtlady "긍휼한 눈을 보였다간 거지 아이들이 달려들 겁니다."
    ch_junghoon "......"
    ch_courtlady "그대는 전하의 명을 받고 왔을 뿐, 소란을 일으켜선 안 됩니다."
    ch_junghoon "명심... 하겠습니다."

    "두 사람은 나루에 닿을 때까지 아무 말도 하지 않았다."

    scene river

    "강가에 배 한 척이 묶여있다."
    "사공은 뱃전에서 꾸벅꾸벅 졸고 있다."
    "상궁은 곧장 다가가 사공에게 일렀다."

    ch_courtlady "이보게!"
    "뱃사공" "...예, 예!"

    "나직이 불렀을 뿐인데, 사공은 천지개벽하는 소리라도 들은 것처럼 놀라 자빠졌다."
    "상궁은 어처구니가 없다는듯 혀를 찼다."

    ch_courtlady "내 분명 언질하였건만, 어찌 잠이나 자고 있단 말인가."
    "뱃사공" "그, 그게... 유시가 다 되도록 오질 않으시니..."
    ch_courtlady "됐네. 어서 이분을 뫼시게."
    "뱃사공" "예, 예... 이쪽으로 타시죠, 나리."

    "‘나리’ 소리에 정훈은 놀란듯 사공을 보았지만,"
    "그 자리에 있는 누구도 그런 걸 신경쓰진 않는 것 같았다."

    ch_junghoon "...나리."

    "정훈은 들릴듯 말듯 독백했다."
    
    return
